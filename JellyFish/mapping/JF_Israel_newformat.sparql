PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct:  <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sosa:  <http://www.w3.org/ns/sosa/> 
PREFIX owl:   <http://www.w3.org/2002/07/owl#> 
PREFIX qudt: <http://qudt.org/schema/qudt/> 
PREFIX unit: <http://qudt.org/vocab/unit/> 
PREFIX sosa:  <http://www.w3.org/ns/sosa/> 
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX wgs84: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX dwc: <http://rs.tdwg.org/dwc/terms/>
PREFIX oim-jf: <https://w3id.org/iliad/oim/ext/jellyfish/>
PREFIX dwc-mbg: <http://mmisw.org/ont/ioos/marine_biogeography/>


### observation collection
CONSTRUCT {
  	?obs_col_URI a sosa:ObservationCollection ;
	dct:identifier ?obsID ;
  	rdfs:label ?obs_col_label ;
	dct:type ?occurrence ;
	sosa:observedProperty ?property_URI ;
	sosa:madeBySensor ?sensor_URI ;
	sosa:hasFeatureOfInterest ?feature_URI ;
	sosa:usedProcedure ?method_URI ;
	sosa:resultTime ?propert_date ;
    sosa:phenomenonTime ?propert_date ; 
	sosa:hasMember ?obs_URI ;
	dwc:eventDate ?propert_date ;
	dwc:eventType ?eventType ;
}  	
FROM <file:jf-pilot-sample.csv> 
WHERE {
	BIND (COALESCE(xsd:dateTime(?eventDate), ?eventDate) AS ?propert_date)	
	BIND (URI(CONCAT('https://w3id.org/iliad/jellyfish/collection/', ?obsID)) AS ?obs_col_URI)
	BIND (STRLANG(CONCAT('Jelly fish observation collection #', ?obsID),"en") AS ?obs_col_label)
	BIND (URI('https://w3id.org/iliad/jellyfish/property/jellyFishAbundanceProperty') AS ?property_URI)
	BIND (URI(CONCAT('https://w3id.org/iliad/jellyfish/sensor/', ?recordedBy)) AS ?sensor_URI)
	BIND (URI(CONCAT('https://w3id.org/iliad/jellyfish/feature/', ?obsID,'-',?Location_20_Zones_ID)) AS ?feature_URI)
	BIND (URI(CONCAT('https://w3id.org/iliad/jellyfish/method/', ?quantificationMethod)) AS ?method_URI)	  
	BIND (URI(CONCAT('https://w3id.org/iliad/jellyfish/observation/', ?occurenceID)) AS ?obs_URI)  
}


### observation 
CONSTRUCT {
  	?obs_URI a sosa:Observation, dwc:HumanObservation;
	dct:identifier ?identifier ;
  	rdfs:label ?obs_label ;  	
  	sosa:hasResult ?result_URI ;
	dwc:occurrenceStatus ?occurrenceStatus ;
	dwc:basisOfRecord ?basisOfRecord ;
	oim-jf:distanceWalkedinmeters ?distanceWalkedinmetersV ;
}  	
FROM <file:jf-pilot-sample.csv> 
WHERE {
	BIND (COALESCE(xsd:dateTime(?datetime), ?datetime) AS ?propert_date)
	BIND (REPLACE(STR(?occurenceID)," ","%20") AS ?identifier)
	BIND (URI(CONCAT('https://w3id.org/iliad/jellyfish/observation/', ?occurenceID)) AS ?obs_URI)
	BIND (STRLANG(CONCAT('Jelly fish observation made by: ', ?recordedBy,' on: ',str(?propert_date),' location: ',?Location_20_Zones_ID,' species: ',?scientificName),"en") AS ?obs_label)	    
	BIND (IF((?scientificName != "-" && ?scientificName != ""),URI(CONCAT('https://w3id.org/iliad/jellyfish/observation/', ?occurenceID,'/result')),URI('https://w3id.org/iliad/jellyfish/observation/emptyResult')) AS ?result_URI)
	BIND (IF((?distanceWalkedinmeters != "-"),?distanceWalkedinmeters,?nothing) AS ?distanceWalkedinmetersV)
}	

### feature of interest
CONSTRUCT {
  	?feature_URI a sosa:FeatureOfInterest, wgs84:Point , dct:Location ;
	dct:identifier ?featureID ;
  	rdfs:label ?feature_label ;
  	wgs84:lat ?decimalLatitude ;
	dwc:decimalLatitude ?decimalLatitude ;
  	wgs84:long ?decimalLongitude ;
	dwc:decimalLongitude ?decimalLongitude ;
  	dwc:locality ?Location_20_Zones_ID ;
  	dwc:coordinateUncertaintyInMeters ?coordinateUncertaintyInMeters ;
  	oim-jf:distanceFromShore ?Distance_from_coast .
}  	
FROM <file:jf-pilot-sample.csv> 
WHERE {  	
	BIND (CONCAT(?obsID,'-',?Location_20_Zones_ID) AS ?featureID)
	BIND (URI(CONCAT('https://w3id.org/iliad/jellyfish/feature/', ?featureID)) AS ?feature_URI)
  	BIND (STRLANG(CONCAT('Feature of interest for JF observation collection: ', ?obsID,' location id: ',?Location_20_Zones_ID),"en") AS ?feature_label)
	#FILTER (?scientificName != "-" && ?scientificName != "")
}

### observed property
CONSTRUCT {
  	?property_URI a sosa:ObservableProperty ;
	dct:identifier "jellyFishAbundanceProperty" ;
  	rdfs:label ?property_label ;
}  	
FROM <file:jf-pilot-sample.csv> 
WHERE {
  	BIND (URI('https://w3id.org/iliad/jellyfish/property/jellyFishAbundanceProperty') AS ?property_URI)	    	    
  	BIND (STRLANG('Jelly Fish Abundance Property',"en") AS ?property_label)
}

### observation result
CONSTRUCT {
  	?result_URI a sosa:Result, oim-jf:JellyFishAbundance ;
	dct:identifier ?identifier ;
  	rdfs:label ?result_label ;	
  	dwc:organismQuantity ?organismQuantity ;
	dwc:organismQuantityType ?organismQuantityType ;	
	dwc:individualCount ?Quantity_Reported ;
  	dwc:sampleSizeValue ?sampleSizeValue; 
	dwc:sampleSizeUnit ?sampleSizeUnit; 
  	dwc:scientificName ?scientificName ;	
    dwc:scientificNameID ?scientificNameID ;
	dwc-mbg:aphiaID ?scientificNameID ;
	oim-jf:stingByJellyFish ?Stinging_Water ;
	oim-jf:strandedJellyfish ?strandedJellyfish ;	
}  	
FROM <file:jf-pilot-sample.csv> 
WHERE {	
	BIND (REPLACE(CONCAT(STR(?occurenceID),'/result')," ","%20") AS ?identifier)
 	BIND (URI(CONCAT('https://w3id.org/iliad/jellyfish/observation/', ?identifier)) AS ?result_URI)	    
  	BIND (STRLANG(CONCAT('Result for jelly fish observation made by: ', ?recordedBy,' on: ',str(?propert_date),' location: ',?Location_20_Zones_ID,' species: ',?scientificName),"en") AS  ?result_label)    
  	FILTER (?scientificName != "-" && ?scientificName != "")
}

### human sensor
CONSTRUCT {
  	?sensor_URI a sosa:Sensor, oim-jf:HumanSensor ;
	dct:identifier ?recordedBy ;
  	rdfs:label ?sensor_label ;
  	oim-jf:agentConfidence ?goldUser ;
}  	
FROM <file:jf-pilot-sample.csv> 
WHERE {
  	BIND (URI(CONCAT('https://w3id.org/iliad/jellyfish/sensor/', ?recordedBy)) AS ?sensor_URI)	
  	BIND (STRLANG(CONCAT('Human sensor: ', ?recordedBy, ' - golden: ',?goldUser),"en") AS ?sensor_label)
	#FILTER (?scientificName != "-" && ?scientificName != "")
}

### activity procedure 
CONSTRUCT {
  	?method_URI a sosa:Procedure ;
	dct:identifier ?identifier ;
  	rdfs:label ?quantificationMethod ;
	dwc:measurementMethod ?quantificationMethod ;
}  	
FROM <file:jf-pilot-sample.csv> 
WHERE {
	BIND (REPLACE(STR(?quantificationMethod)," ","%20") AS ?identifier)
	BIND (URI(CONCAT('https://w3id.org/iliad/jellyfish/method/', ?quantificationMethod)) AS ?method_URI)	    	
	#FILTER (?scientificName != "-" && ?scientificName != "")
}